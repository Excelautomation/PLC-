Package dk.aau.sw402F15.parser;

Helpers
ht  = 0x0009;
lf  = 0x000a;
ff  = 0x000c;
cr  = 0x000d;
sp  = ' ';

line_terminator = lf | cr | cr lf;

all = [0 .. 255];

digit =	['0' .. '9'];
letter = ['a' .. 'z'] | ['A' .. 'Z'];
specials = '_';

letter_or_digit = (letter | digit);
letter_or_specials = (letter | specials);
letter_or_digit_or_specials = (letter_or_digit | specials);

not_line_terminator = [all - [cr + lf]];

Tokens
comments = ('//' not_line_terminator* line_terminator);

l_par = '(';
r_par = ')';
l_brc = '{';
r_brc = '}';
l_bkt = '[';
r_bkt = ']';

semi = ';';
colon = ':';
comma = ',';
dot = '.';

// Operators
add_assignment_operator = '+=';
sub_assignment_operator = '-=';
mult_assignment_operator = '*=';
div_assignment_operator = '/=';
mod_assignment_operator = '%=';
assignment_operator = '=';

increment_operator = '++';
decrement_operator = '--';

equal_operator = '==';
not_equal_operator = '!=';
and_operator = '&&';
or_operator = '||';

ternary_operator = '?';

add_operator = '+';
sub_operator = '-';
mult_operator = '*';
div_operator = '/';
mod_operator = '%';
negate_operator = '!';

greater_than_operator = '>';
less_than_operator = '<';
greater_than_or_equal_operator = '>=';
less_than_or_equal_operator = '<=';

// Keywords
char_keyword        = 'char';
bool_keyword        = 'bool';
int_keyword         = 'int';
long_keyword        = 'long';
double_keyword      = 'double';
float_keyword       = 'float';
default_keyword     = 'default';
switch_keyword      = 'switch';
case_keyword        = 'case';
break_keyword       = 'break';
continue_keyword    = 'continue';
const_keyword       = 'const';
enum_keyword        = 'enum';
port_keyword        = 'port';
timer_keyword       = 'timer';
void_keyword        = 'void';
return_keyword      = 'return';
struct_keyword      = 'struct';

if_keyword = 'if';
else_keyword = 'else';

for_keyword = 'for';
while_keyword = 'while';

true_keyword = 'true';
false_keyword = 'false';

// Literal
integer_literal = (digit)+;
decimal_literal = (digit)+ '.' (digit)+;

// Identifier
port_input_identifier = 'I#';
port_output_identifier = 'O#';
port_memory_identifier = 'M#';
port_analog_input_identifier = 'AI#';
port_analog_output_identifier = 'AO#';
identifier = letter_or_specials letter_or_digit_or_specials*;

// White space
white_space = (sp | ht | ff | line_terminator)+;

Ignored Tokens
	comments,
	white_space;

Productions

program {-> program} =
	root_declaration*																									{-> New program([root_declaration])}
;

// Types
type_specifier {-> type_specifier} = 
    {char} char_keyword																									{-> New type_specifier.char()}
  | {bool} bool_keyword																									{-> New type_specifier.bool()}
  | {int} int_keyword																									{-> New type_specifier.int()}
  | {long} long_keyword																									{-> New type_specifier.long()}
  | {float} float_keyword																								{-> New type_specifier.float()}
  | {double} double_keyword																								{-> New type_specifier.double()}
  | {timer} timer_keyword																								{-> New type_specifier.timer()}
  | {port} port_keyword																									{-> New type_specifier.port()}
  | {enum} enum_keyword identifier                                                                                      {-> New type_specifier.enum(identifier)}
  | {struct} struct_keyword identifier																				    {-> New type_specifier.struct(identifier)}
;

// Declaration in root scope
root_declaration {-> root_declaration} =
	{variable} declaration																								{-> New root_declaration.declaration(declaration.declaration)}
  |	{function} function_definition																						{-> function_definition.root_declaration}
  | {struct} struct_definition																							{-> struct_definition.root_declaration}
  | {enum} enum_definition                                                                                              {-> enum_definition.root_declaration}
;

// Function definition
function_definition {-> root_declaration} =
	{function} type_specifier identifier l_par formal_parameter? r_par l_brc statement* r_brc							{-> New root_declaration.function(type_specifier, identifier, [formal_parameter.declaration], [statement])}
  | {method} void_keyword identifier l_par formal_parameter? r_par l_brc statement* r_brc								{-> New root_declaration.function(New type_specifier.void(), identifier, [formal_parameter.declaration], [statement])}
;

// Formal parameters
formal_parameter {-> declaration*} =
    {single} type_qualifier? type_specifier identifier array_definition?											    {-> [New declaration(type_specifier, identifier, type_qualifier.type_qualifier, array_definition.array_definition, Null)]}
  | {multiple} type_qualifier? type_specifier identifier array_definition? comma formal_parameter						{-> [New declaration(type_specifier, identifier, type_qualifier.type_qualifier, array_definition.array_definition, Null), formal_parameter.declaration]}
;

// Struct
struct_definition {-> root_declaration} =
	struct_keyword identifier l_brc struct_declaration* r_brc														    {-> New root_declaration.struct(identifier, New program([struct_declaration.root_declaration]))}
;

struct_declaration {-> root_declaration} =
    {variable} declaration																								{-> New root_declaration.declaration(declaration.declaration)}
  |	{function} function_definition																						{-> function_definition.root_declaration}
;

// Enum
enum_definition {-> root_declaration} =
	enum_keyword identifier l_brc enum_flags r_brc																		{-> New root_declaration.enum(identifier, [enum_flags.enum_flag])}
;

enum_flags {-> enum_flag*} =
    {single} enum_flag                                                                                                  {-> [enum_flag.enum_flag]}
  | {multiple} enum_flag comma enum_flags                                                                               {-> [enum_flag.enum_flag, enum_flags.enum_flag]}
;

enum_flag {-> enum_flag} =
    {simple} identifier                                                                                                 {-> New enum_flag(identifier, Null)}
  | {value} identifier assignment_operator integer_literal                                                              {-> New enum_flag(identifier, integer_literal)}
;

// Type-qualifier
type_qualifier {-> type_qualifier} =
    {const} const_keyword                                                                                               {-> New type_qualifier.const()}
;

// Declaration
declaration {-> declaration} =
	{simple} type_qualifier? type_specifier identifier array_definition? semi											{-> New declaration(type_specifier.type_specifier, identifier, type_qualifier.type_qualifier, array_definition.array_definition, Null)}
  | {assignment} type_qualifier? type_specifier identifier array_definition? assignment_operator expr semi				{-> New declaration(type_specifier.type_specifier, identifier, type_qualifier.type_qualifier, array_definition.array_definition, expr)}
;

array_definition {-> array_definition} =
    l_bkt [number]:integer_literal? r_bkt                                                                               {-> New array_definition(number)}
;

// Statements
statement {-> statement} =
    // Scope
    {scope} l_brc statement* r_brc																						{-> New statement.scope([statement.statement])}

    // Declaration and expr
  |	{declaration} declaration																							{-> New statement.declaration(declaration.declaration)}
  |	{expr} expr	semi																									{-> New statement.expr(expr.expr)}
  
  // Conditional
  | {if} if_statement																									{-> if_statement.statement}
  | {switch} switch_statement                                                                                           {-> switch_statement.statement}

  // Iteration
  | {while} while_keyword l_par [condition]:expr r_par statement														{-> New statement.while(condition.expr, statement.statement)}
  | {for} for_keyword l_par [initilizer]:expr? [s1]:semi [condition]:expr? [s2]:semi [iterator]:expr? r_par statement	{-> New statement.for(initilizer.expr, condition.expr, iterator.expr, statement.statement)} // TODO: for with declaration
  
  // Control
  | {return} return_keyword semi																						{-> New statement.return()}
  | {return_expr} return_keyword expr semi																				{-> New statement.return_expr(expr.expr)}
  | {continue} continue_keyword semi                                                                                    {-> New statement.continue()}
  | {break} break_keyword semi                                                                                          {-> New statement.break()}
;

statement_no_short_if  {-> statement} =
    // Scope
    {scope} l_brc statement* r_brc																			            {-> New statement.scope([statement.statement])}

    // Declaration and expr
  |	{declaration} declaration																							{-> New statement.declaration(declaration.declaration)}
  |	{expr} expr	semi																									{-> New statement.expr(expr.expr)}

  // Conditional
  | {if} if_statement_short																								{-> if_statement_short.statement}
  | {switch} switch_statement                                                                                           {-> switch_statement.statement}

  // Iteration
  | {while} while_keyword l_par [condition]:expr r_par statement_no_short_if											{-> New statement.while(condition.expr, statement_no_short_if.statement)}
  | {for} for_keyword l_par [initilizer]:expr? [s1]:semi [condition]:expr? [s2]:semi [iterator]:expr?
            r_par statement_no_short_if	                                                                                {-> New statement.for(initilizer.expr, condition.expr, iterator.expr, statement_no_short_if.statement)} // TODO: for with declaration

  // Control
  | {return} return_keyword semi																						{-> New statement.return()}
  | {return_expr} return_keyword expr semi																				{-> New statement.return_expr(expr.expr)}
  | {continue} continue_keyword semi                                                                                    {-> New statement.continue()}
  | {break} break_keyword semi                                                                                          {-> New statement.break()}
;

// If
if_statement {-> statement} =
    {if_statement} if_keyword l_par [condition]:expr r_par statement													{-> New statement.branch(condition.expr, statement.statement, Null)}
  | {if_else} if_keyword l_par [condition]:expr r_par [if]:statement_no_short_if else_keyword [else]:statement	        {-> New statement.branch(condition.expr, if.statement, else.statement)}
;

if_statement_short {-> statement} =
    {if_else} if_keyword l_par expr r_par [if]:statement_no_short_if else_keyword [else]:statement_no_short_if	        {-> New statement.branch(expr.expr, if.statement, else.statement)}
;

// Switch
switch_statement {-> statement} =
    {switch} switch_keyword l_par [condition]:expr r_par l_brc label_statements* r_brc                                  {-> New statement.switch(condition.expr, [label_statements.statement])}
;

label_statements {-> statement} =
    {case} case_keyword [condition]:expr colon statement*                                                               {-> New statement.case(condition.expr, [statement.statement])}
  | {default} default_keyword colon statement*                                                                          {-> New statement.default([statement.statement])}
;

// Expression
// C operator precedence - precedence inverted
// Precedence 14
expr {-> expr} =
	// Assignment
	{assignment} [left]:expr assignment_operator [right]:ternary_expr															{-> New expr.assignment(left.expr, right.expr)}
	
	// Compound
  |	{compound_add} [left]:expr add_assignment_operator [right]:ternary_expr													{-> New expr.compound_add(left.expr, right.expr)}
  | {compound_sub} [left]:expr sub_assignment_operator [right]:ternary_expr													{-> New expr.compound_sub(left.expr, right.expr)}
  | {compound_mult} [left]:expr mult_assignment_operator [right]:ternary_expr													{-> New expr.compound_mult(left.expr, right.expr)}
  | {compound_div} [left]:expr div_assignment_operator [right]:ternary_expr													{-> New expr.compound_div(left.expr, right.expr)}
  | {compound_mod} [left]:expr mod_assignment_operator [right]:ternary_expr													{-> New expr.compound_mod(left.expr, right.expr)}
  | {factor} [expr]:ternary_expr																								{-> expr.expr}
;

// Precedence 13 (Ternary conditional)
ternary_expr {-> expr} =
    {ternary} [condition]:ternary_expr ternary_operator [left]:or_expr colon [right]:or_expr                                    {-> New expr.ternary(condition.expr, left.expr, right.expr)}
  | {factor} [expr]:or_expr                                                                                              {-> expr.expr}
;

// Precedence 12 (Logical OR)
or_expr {-> expr} =
	{or} [left]:or_expr or_operator [right]:and_expr																		{-> New expr.compare_or(left.expr, right.expr)}
  | {factor} [expr]:and_expr																								{-> expr.expr}
;

// Precedence 11 (Logical AND)
and_expr {-> expr} =
	{and} [left]:and_expr and_operator [right]:equality_expr																		{-> New expr.compare_and(left.expr, right.expr)}
  | {factor} [expr]:equality_expr																								{-> expr.expr}
;

// Precedence 7 (For relational and respectively)
equality_expr {-> expr} =
	{equal} [left]:equality_expr equal_operator [right]:greater_less_expr																	{-> New expr.compare_equal(left.expr, right.expr)}
  |	{not_equal} [left]:equality_expr not_equal_operator [right]:greater_less_expr															{-> New expr.compare_not_equal(left.expr, right.expr)}
  | {factor} [expr]:greater_less_expr																								{-> expr.expr}
;

// Precedence 6 (For relational operators and respectively)
greater_less_expr {-> expr} =
	{less} [left]:greater_less_expr less_than_operator [right]:add_sub_expr																{-> New expr.compare_less(left.expr, right.expr)}
  |	{less_or_equal} [left]:greater_less_expr less_than_or_equal_operator [right]:add_sub_expr												{-> New expr.compare_less_or_equal(left.expr, right.expr)}
  |	{greater} [left]:greater_less_expr greater_than_operator [right]:add_sub_expr															{-> New expr.compare_greater(left.expr, right.expr)}
  |	{greater_or_equal} [left]:greater_less_expr greater_than_or_equal_operator [right]:add_sub_expr										{-> New expr.compare_greater_or_equal(left.expr, right.expr)}
  | {factor} [expr]:add_sub_expr																								{-> expr.expr}
;

// Precedence 4 (Addition and subtraction)
add_sub_expr {-> expr} =
	{add} [left]:add_sub_expr add_operator [right]:mult_div_mod_expr																		{-> New expr.add(left.expr, right.expr)}
  |	{sub} [left]:add_sub_expr sub_operator [right]:mult_div_mod_expr																		{-> New expr.sub(left.expr, right.expr)}
  | {factor} [expr]:mult_div_mod_expr																								{-> expr.expr}
;

// Precedence 3 (Multiplication, division, and remainder)
mult_div_mod_expr {-> expr} =
	{multi} [left]:mult_div_mod_expr mult_operator [right]:unary_prefix_expr																	{-> New expr.multi(left.expr, right.expr)}
  |	{div} [left]:mult_div_mod_expr div_operator [right]:unary_prefix_expr																		{-> New expr.div(left.expr, right.expr)}
  |	{mod} [left]:mult_div_mod_expr mod_operator [right]:unary_prefix_expr																		{-> New expr.mod(left.expr, right.expr)}
  | {factor} [expr]:unary_prefix_expr																								{-> expr.expr}
;

// Precedence 2
unary_prefix_expr {-> expr} =
	// Prefix increment / decrement
	{increment} increment_operator identifier																			{-> New expr.increment(identifier)}
  |	{decrement} decrement_operator identifier																			{-> New expr.decrement(identifier)}
	
	// Unary plus and minus
  |	{unary_plus} add_operator [expr]:unary_prefix_expr																				{-> New expr.unary_plus(expr.expr)}
  |	{unary_minus} sub_operator [expr]:unary_prefix_expr																				{-> New expr.unary_minus(expr.expr)}

	// Negation
  | {negate} negate_operator [expr]:unary_prefix_expr																				{-> New expr.negation(expr.expr)}
  
	// Type cast
  | {cast} l_par [type]:type_specifier r_par [expr]:unary_prefix_expr																{-> New expr.type_cast(type.type_specifier, expr.expr)}
  
  | {factor} [expr]:post_and_infix_expr																								{-> expr.expr}
;

// Precedence 1
post_and_infix_expr {-> expr} =
  	// Postfix increment / decrement
	{increment} identifier increment_operator																			{-> New expr.increment(identifier)}
  |	{decrement} identifier decrement_operator																			{-> New expr.decrement(identifier)}
  
	// Array
  | {array_subscript} identifier l_bkt [expr]:post_and_infix_expr r_bkt																{-> New expr.array(identifier, expr.expr)}
  
	// Dot
  | {member} [left]:post_and_infix_expr dot [right]:expr_value																		{-> New expr.member(left.expr, right.expr)}
  
	// Parenthesis
  | {par} l_par expr r_par																								{-> expr.expr}
  
  | {factor} [expr]:expr_value																							{-> expr.expr}
;

// Function call
function_call {-> expr} =
  	identifier l_par actual_parameter? r_par																			{-> New expr.function_call(identifier, [actual_parameter.expr])}
;
  
// Actual parameters
actual_parameter {-> expr*} =
	{single} expr																										{-> [expr.expr]}
  | {multiple} expr comma actual_parameter																				{-> [expr.expr, actual_parameter.expr]}
;

expr_value {-> expr} =
    {integer}                         integer_literal                                                                   {-> New expr.integer(integer_literal)}
  | {decimal}                         decimal_literal                                                                   {-> New expr.decimal(decimal_literal)}
  | {true}                            true_keyword                                                                      {-> New expr.true()}
  | {false}                           false_keyword                                                                     {-> New expr.false()}
  | {identifier}				      identifier																	    {-> New expr.identifier(identifier)}

  | {port_input_identifier}		      port_input_identifier [expr]:port_value											{-> New expr.port_input(expr.expr)}
  | {port_output_identifier}	      port_output_identifier [expr]:port_value											{-> New expr.port_output(expr.expr)}
  | {port_memory_identifier}	      port_memory_identifier [expr]:port_value											{-> New expr.port_memory(expr.expr)}
  | {port_analog_input_identifier}	  port_analog_input_identifier [expr]:port_value									{-> New expr.port_analog_input(expr.expr)}
  | {port_analog_output_identifier}   port_analog_output_identifier [expr]:port_value									{-> New expr.port_analog_output(expr.expr)}
	
  // Precedence changed from C to allow functions in structs
  // Function call
  | {function_call} function_call																						{-> function_call.expr}
;

port_value {-> expr} =
    {value} [expr]:expr_value                                                                                           {-> expr.expr}
  | {par} l_par [expr]:expr r_par                                                                                       {-> expr.expr}
;

Abstract Syntax Tree

program =
	root_declaration*
;

root_declaration = 
	{declaration} 				declaration 
  |	{function} 					[return_type]:type_specifier	[name]:identifier			[params]:declaration*		[statements]:statement*
  | {struct}					[name]:identifier				[program]:program
  | {enum}                      [name]:identifier				[program]:enum_flag*
;

enum_flag =
    identifier                  [value]:integer_literal?
;
  
declaration =
						        [type]:type_specifier			[name]:identifier           [quailifer]:type_qualifier? [array]:array_definition?   [expr]:expr?
;

array_definition =
                                [number]:integer_literal?
;

type_specifier =
    {char}
  | {bool}
  | {int}
  | {long}
  | {float}
  | {double}
  | {timer}
  | {port}
  | {enum}                      identifier
  | {struct}                    identifier
  | {void}
;

type_qualifier =
    {const}
;

statement =
	{declaration} 				declaration
  | {expr} 						expr
  | {scope}					    statement*
  
  // Conditional
  | {branch}					[condition]:expr				[left]:statement?		    [right]:statement?

  // Switch
  | {switch}                    [condition]:expr                [labels]:statement*
  | {case}                      [case]:expr                     [statement]:statement*
  | {default}                                                   [statement]:statement*

  // Iteration
  | {while}						[condition]:expr				[statement]:statement
  | {for}						[initilizer]:expr?				[condition]:expr?			[iterator]:expr?            [statement]:statement
  
  // Control-statements
  | {return}
  | {return_expr}				expr
  | {continue}
  | {break}
;

expr =
  | {identifier}				[name]:identifier

  | {function_call}				[name]:identifier				[args]:expr*
  | {array}						[name]:identifier				expr
  | {member}					[left]:expr						[right]:expr

  | {increment}					[name]:identifier
  | {decrement}					[name]:identifier
  | {unary_plus}				expr
  | {unary_minus}				expr
  | {negation}					expr
  | {type_cast}					[target_type]:type_specifier	expr
  
  | {multi}						[left]:expr						[right]:expr
  | {div}						[left]:expr						[right]:expr
  | {mod}						[left]:expr						[right]:expr

  | {add}						[left]:expr						[right]:expr
  | {sub}						[left]:expr						[right]:expr

  | {compare_less}				[left]:expr						[right]:expr
  | {compare_less_or_equal}		[left]:expr						[right]:expr
  | {compare_greater}			[left]:expr						[right]:expr
  | {compare_greater_or_equal}	[left]:expr						[right]:expr

  | {compare_equal}				[left]:expr						[right]:expr
  | {compare_not_equal}			[left]:expr						[right]:expr
  
  | {compare_and}				[left]:expr						[right]:expr
  | {compare_or}				[left]:expr						[right]:expr
  | {assignment}				[left]:expr				        [right]:expr
  | {compound_add}				[left]:expr				        [right]:expr
  | {compound_sub}				[left]:expr				        [right]:expr
  | {compound_mult}				[left]:expr				        [right]:expr
  | {compound_div}				[left]:expr				        [right]:expr
  | {compound_mod}				[left]:expr				        [right]:expr

  | {ternary}                   [condition]:expr                [left]:expr             [right]:expr

  |	{integer}                   integer_literal
  | {decimal}                   decimal_literal
  | {true}
  | {false}

  | {port_input}                expr
  | {port_output}               expr
  | {port_memory}               expr
  | {port_analog_input}         expr
  | {port_analog_output}        expr
;