Package dk.aau.sw402F15.parser;

Helpers
ht  = 0x0009;
lf  = 0x000a;
ff  = 0x000c;
cr  = 0x000d;
sp  = ' ';

line_terminator = lf | cr | cr lf;

digit =	['0' .. '9'];
letter = ['a' .. 'z'] | ['A' .. 'Z'];

letter_or_digit = (letter | digit);

Tokens
l_par = '(';
r_par = ')';
l_brc = '{';
r_brc = '}';
semi = ';';
comma = ',';

// Operators
add_assignment_operator = '+=';
sub_assignment_operator = '-=';
mult_assignment_operator = '*=';
div_assignment_operator = '/=';
mod_assignment_operator = '%=';
assignment_operator = '=';

increment_operator = '++';
decrement_operator = '--';

add_operator = '+';
sub_operator = '-';
mult_operator = '*';
div_operator = '/';
mod_operator = '%';

equal_operator = '==';
not_equal_operator = '!=';
and_operator = '&&';
or_operator = '||';

greater_than_operator = '>';
less_than_operator = '<';
greater_than_or_equal_operator = '>=';
less_than_or_equal_operator = '<=';

// Keywords
byte_keyword = 'byte';
bool_keyword = 'bool';
int_keyword = 'int';
long_keyword = 'long';
float_keyword = 'float';
double_keyword = 'double';
port_keyword = 'port';
void_keyword = 'void';

if_keyword = 'if';
else_keyword = 'else';

for_keyword = 'for';
while_keyword = 'while';

true_keyword = 'true';
false_keyword = 'false';

// Literal
integer_literal = (digit)+;
decimal_literal = (digit)+ '.' (digit)+;

// Identifier
identifier = letter letter_or_digit*;

// White space
white_space = (sp | ht | ff | line_terminator)+;

Ignored Tokens
white_space;

Productions

program =
  {statement} statement program? |
  {function} function_declaration program?;

// Types
type = 
  {byte} byte_keyword | 
  {bool} bool_keyword | 
  {int} int_keyword | 
  {long} long_keyword | 
  {float} float_keyword | 
  {double} double_keyword | 
  {port} port_keyword;
  
// Scope
scope = 
  l_brc statements? r_brc;
  
// Function declaration
functions =
  function_declaration functions?;

function_declaration =
  {with_return} type identifier l_par parameters? r_par scope |
  {no_return} void_keyword identifier l_par parameters? r_par scope;

// Function call
function_call =
  identifier l_par parameters? r_par semi;
  
// Parameters
parameters =
  parameter additional_parameter?;

additional_parameter = 
  comma parameter;
  
parameter = 
  type identifier;
  
// Statements
statements = 
  {statement} statement statements? |
  {function} function_call statements?;  

statement = 
  {declaration} declaration_statement semi |
  {assignment} assignment_statement semi |
  {conditional} conditional_statement |
  {scope} scope;

// Declaration
declaration_statement =
  {declaration} type identifier |
  {declaration_assignment} type identifier assignment_operator expr;

// Assignment  
assignment_statement = 
  {assignment} identifier assignment_operator expr |
  {increment_prefix} increment_operator identifier |
  {increment_suffix} identifier increment_operator |
  {decrement_prefix} decrement_operator identifier |
  {decrement_suffix} identifier decrement_operator |
  {add} identifier add_assignment_operator expr |
  {sub} identifier sub_assignment_operator expr |
  {mult} identifier mult_assignment_operator expr |
  {div} identifier div_assignment_operator expr |
  {mod} identifier mod_assignment_operator expr;
  
// Conditional statement
conditional_statement =
  {if} if_statement;

if_statement =
  {if} if_keyword l_par expr r_par scope |
  {if_else} if_keyword l_par expr r_par scope else_statement;

else_statement = 
  {else} else_keyword scope |
  {else_if} else_keyword if_statement;

// Iterative statement
iterative_statement =
  {while} while_statement |
  {for} for_statement;
  
while_statement = 
  {while} while_keyword l_par expr r_par scope;

for_statement =
  {for} for_keyword l_par for_initializer for_condition for_iterator r_par scope;
  
for_initializer =
  {assignment} assignment_statement semi |
  {declaration} declaration_statement semi;

for_condition =
  {expr} expr semi;
  
for_iterator =
  {iterator} assignment_statement;
  
// Expression
expr = 
  {compare_and} expr and_operator expr2 |
  {compare_or} expr or_operator expr2 |
  {term} expr2;

expr2 =
  {compare_equal} expr2 equal_operator expr3 | 
  {compare_not_equal} expr2 not_equal_operator expr3 | 
  {term} expr3;

expr3 =
  {compare_greater} expr3 greater_than_operator expr4 |
  {compare_less} expr3 less_than_operator expr4 |
  {compare_greater_or_equal} expr3 greater_than_or_equal_operator expr4 |
  {compare_less_or_equal} expr3 less_than_or_equal_operator expr4 |
  {term} expr4;

expr4 =
  {add} expr4 add_operator expr5 |
  {sub} expr4 sub_operator expr5 |
  {term} expr5;

expr5 =
  {multi} expr5 mult_operator expr_value |
  {div} expr5 div_operator expr_value |
  {mod} expr5 mod_operator expr_value |
  {term} expr_value;

expr_value = 
  {true} true_keyword | 
  {false} false_keyword |
  {value} value |
  {identifier} identifier |
  {expr} l_par expr r_par;
  
value =
  {number} number |
  {negnumber} sub_operator number;
  
number =
  {integer} integer_literal |
  {decimal} decimal_literal;
